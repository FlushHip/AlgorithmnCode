# x86函数调用惯例

## 概览

- `__cdecl`是C/C++程序默认的调用惯例，这种调用惯例的优点是允许函数使用可变数量的参数，缺点是程序会变大；
- `__stdcall`是`WINAPI`的定义，被用于Win32的API函数，它不允许函数使用可变参数；
- `__fastcall`是把参数塞入寄存器，而不压入栈中，寄存器的操作速度快于压栈，因此会使函数调用加快；
- `thiscall`是C++成员函数的调用惯例，同样是不允许使用可变参数的。

## 当一个函数被调用时，发生了啥

不管是哪种调用惯例，以下的事情将会发生：

1. 所有参数被扩展到4字节的倍数（以Win32平台为例），然后把参数放入到恰当的内存位置，有可能是栈，也有可能是寄存器。当然，这由调用惯例规定；
2. 程序跳转到被调用函数的地址处；
3. 堆栈指针`ebp`等等寄存器保存于栈上，执行这步操作的代码叫做*function prolog*，每个函数前都会有这么一部分代码，这些代码由编译器生成；
4. 函数本身被执行，之后，把返回值置于`eax`寄存器，函数返回；
5. 第三步被保存的寄存器从栈中恢复，执行这步操作的代码叫做*function epilog*，每个函数前都会有这么一部分代码，这些代码由编译器生成；
6. 所有参数从栈上移除，这步操作叫**堆栈清理**（也叫**堆栈平衡**），这步操作可以由调用者完成，也可以由被调用者完成，这取决与函数调用惯例。

在具体代码上添加具体调用惯例来展示步骤，会以Win32的汇编作为演示，演示的时候都是使用C语言，这里不使用C++是为了避免C++的名字修饰。

## 何为调用惯例

当函数被调用时，需要传递参数给函数，同时还需取回返回值。调用惯例描述**参数如何传递给函数和返回值如何返回**，同时它还描述**函数名如何被修饰**。平时你可能接触不到调用惯例，但是如果你是库的作者，或者调试，或者反编译时，调用惯例就显得很重要了。

由于绝大部分调用惯例传参都是通过压栈的方式（`__fastcall`通过寄存器传参），因此，**栈的清理（堆栈平衡）的工作到底是由调用者来完成还是由被调用者来完成**，这也是调用惯例所规定的。

## `__cdecl`调用惯例

Win32编译时加入`/Gd`，或者直接`int __cdecl fun();`，即可设置`__cdecl`调用惯例，不过一般都是不用设置的，默认就是`__cdecl`调用惯例，我使用CMake生成的项目，语句如下：

```cmake
add_executable(call_convention main.c)

target_compile_options(call_convention
    PRIVATE $<$<CXX_COMPILER_ID:Clang,AppleClang,GNU,ARMClang>:-save-temps=obj -masm=intel>
            $<$<CXX_COMPILER_ID:MSVC>:/Gd>)
```

`main.c`的代码如下：

```c
int fun_1(char c, int a)
{
    int tmp = c + a;
}

int fun_2(char c, int a)
{
    int tmp = c - a;
}

int main(int argc, char** argv)
{
    int a = 1, b = 2, c = 0;
    c = a + b;
    char d = 0;
    int used = fun_1(d, c);
    used = fun_2(d, d);
    return 0;
}
```

汇编代码如下：

```assembly
int fun_1(char c, int a)
{
 push        ebp  
 mov         ebp,esp  
 push        ecx  
 mov         dword ptr [tmp],0CCCCCCCCh  
    int tmp = c + a;
 movsx       eax,byte ptr [c]  
 add         eax,dword ptr [a]  
 mov         dword ptr [tmp],eax  
}
 mov         esp,ebp  
 pop         ebp  
 ret  

int fun_2(char c, int a)
{
; 3. function prolog，上一栈帧的栈底寄存器ebp入栈，然后再把上一栈帧的栈顶esp复制给ebp表示这个被调用的函数的栈开始建设
;    如果这个函数继续有函数调用，那么esp要增大到合适位置，(ebp - esp)就是开出来的栈内存大小
 push        ebp  
 mov         ebp,esp  
 push        ecx  
 mov         dword ptr [tmp],0CCCCCCCCh  
    int tmp = c - a;
; 4. 执行函数自己的逻辑，同时把返回值置于eax
 movsx       eax,byte ptr [c]  
 sub         eax,dword ptr [a]  
 mov         dword ptr [tmp],eax  
}
; 5. function epilog，函数退栈，回复上一栈帧的栈顶指针esp，栈底指针ebp
 mov         esp,ebp  
 pop         ebp  
 ret  

int main(int argc, char** argv)
{
 push        ebp  
 mov         ebp,esp  
 sub         esp,14h  ; 3. 开栈，由于内存对齐，5个变量，因此需要开栈空间20个字节，即14h
 mov         eax,0CCCCCCCCh  
 mov         dword ptr [used],eax  
 mov         dword ptr [ebp-10h],eax  
 mov         dword ptr [c],eax  
 mov         dword ptr [b],eax  
 mov         dword ptr [a],eax  
    int a = 1, b = 2, c = 0;
 mov         dword ptr [a],1  
 mov         dword ptr [b],2  
 mov         dword ptr [c],0  
    c = a + b;
 mov         eax,dword ptr [a]  
 add         eax,dword ptr [b]  
 mov         dword ptr [c],eax  
    char d = 0;
 mov         byte ptr [d],0  
    int used = fun_1(d, c);
 mov         ecx,dword ptr [c]  ; 1. char类型被压入4字节的寄存器中，对应于第一条，所有参数都扩展为4字节长度
 push        ecx  ; 1. 参数压栈
 movzx       edx,byte ptr [d]  
 push        edx  
 call        _fun_1 (0DF124Eh)  ; 2. 程序跳转
 add         esp,8  ; 6. 堆栈平衡，也叫堆栈清理，由于传入两个参数，因此需要回退8个字节的空间，fun_1的栈空间等待被下一栈帧复用
 mov         dword ptr [used],eax  6. 从eax拿到返回值，如果没有返回值，通常会执行xor eax eax，把eax清零
    used = fun_2(d, d);
 movsx       eax,byte ptr [d]  
 push        eax  
 movzx       ecx,byte ptr [d]  
 push        ecx  
 call        _fun_2 (0DF1235h)  
 add         esp,8  
 mov         dword ptr [used],eax  
    return 0;
 xor         eax,eax  
}
 add         esp,14h  
 cmp         ebp,esp  
 call        __RTC_CheckEsp (0DF1154h)  
 mov         esp,ebp  
 pop         ebp  
 ret  
```

从这个汇编代码来分析，可以看到：

- 参数的压栈顺序是从右到左；
- 堆栈平衡由调用者完成；
- 函数名称的修饰规则是添加前置`_`。

## `__stdcall`调用惯例

```c
#define CALLBACK    __stdcall
#define WINAPI      __stdcall
```

在Window的头文件中可以找到这么几行代码，可以看到，Win32 API和回调函数都是使用`__stdcall`调用惯例。

项目CMake语句如下：

```cmake
add_executable(call_convention main.c)

target_compile_options(call_convention
    PRIVATE $<$<CXX_COMPILER_ID:Clang,AppleClang,GNU,ARMClang>:-save-temps=obj -masm=intel>
            $<$<CXX_COMPILER_ID:MSVC>:/Gz>)
```

这里展示一部分与`__cdecl`不同的汇编代码：

```assembly
int fun_1(char c, int a)
{
 push        ebp  
 mov         ebp,esp  
 push        ecx  
 mov         dword ptr [tmp],0CCCCCCCCh  
    int tmp = c + a;
 movsx       eax,byte ptr [c]  
 add         eax,dword ptr [a]  
 mov         dword ptr [tmp],eax  
}
 mov         esp,ebp  
 pop         ebp  
 ret         8	; 堆栈平衡者变了，参数栈的空间大小为8个字节，因此ret 8

;......

int used = fun_1(d, c);
 mov         ecx,dword ptr [c]  
 push        ecx  
 movzx       edx,byte ptr [d]  
 push        edx  
 call        _fun_1@8 (0FB109Bh)  ; 函数修饰规则变了
 mov         dword ptr [used],eax
```

可以看到`__stdcall`与`__cdecl`的相同之处和不同之处

- 相同
  - 参数压栈的顺序都是从右到左；
  - *function prolog* 和 *function epilog*一样。
- 不同
  - `__stdcall`的堆栈平衡由被调用者完成；
  - `__stdcall`的函数名称修饰规则是添加前置`_`，并且末尾添加`@`并且加上函数的参数栈所需空间的字节数，这里是2个参数，8个字节。

由于堆栈平衡由被调用的函数完成，因此堆栈平衡的那部分代码本分散到各个函数，所以会使得生成的程序体积会变小；同时，也是因为堆栈平衡由被调用者完成，因此`__stdcall`是不支持可变参数函数的，因为一个函数传入多少个参数进来，被调用者是不知道的，被调用者只能根据参数的提示到栈中自己去找（因此编译器不知道具体参数数量，这个参数数量需要由开发者去找到），因此啊，`__stdcall`就做不了可变参数函数的堆栈平衡，而唯一知道传入参数数量的必然就是调用者，自然而然，堆栈平衡就可以完成了，而`__cdecl`的堆栈平衡恰巧就是在调用者这一方完成的。

这里可以看下`printf`的声明，恰巧就验证了上诉说明。

```c
#ifdef _M_CEE_PURE
    #define __CLRCALL_PURE_OR_CDECL __clrcall   // 忽略
#else
    #define __CLRCALL_PURE_OR_CDECL __cdecl
#endif

#define __CRTDECL __CLRCALL_PURE_OR_CDECL

_Check_return_opt_
    _CRT_STDIO_INLINE int __CRTDECL printf(
    _In_z_ _Printf_format_string_ char const* const _Format,
    ...)
```

## `__fastcall`调用惯例

项目CMake代码：

```cmake
add_executable(call_convention main.c)

target_compile_options(call_convention
    PRIVATE $<$<CXX_COMPILER_ID:Clang,AppleClang,GNU,ARMClang>:-save-temps=obj -masm=intel>
            $<$<CXX_COMPILER_ID:MSVC>:/Gr>)
```

生成的汇编代码：

```assembly
int fun_1(char c, int a, int b, int d)
{
 push        ebp  
 mov         ebp,esp  
 sub         esp,0Ch  
 mov         dword ptr [tmp],0CCCCCCCCh  
 mov         dword ptr [a],0CCCCCCCCh  
 mov         dword ptr [c],0CCCCCCCCh  
 mov         dword ptr [a],edx  
 mov         byte ptr [c],cl  
    int tmp = c + a + b + d;
 movsx       eax,byte ptr [c]  
 add         eax,dword ptr [a]  
 add         eax,dword ptr [b]  
 add         eax,dword ptr [d]  
 mov         dword ptr [tmp],eax  
}
 mov         esp,ebp  
 pop         ebp  
 ret         8
 
; ......

   int used = fun_1(d, a, b, c);
 mov         ecx,dword ptr [c]  
 push        ecx  
 mov         edx,dword ptr [b]  
 push        edx  
 mov         edx,dword ptr [a]  
 mov         cl,byte ptr [d]  
 call        @fun_1@16 (06121Ch)  
 mov         dword ptr [used],eax
```

可以看到，`__fastcall`基本是和`__stdcall`一样的。只不过函数的前两个参数如果是4个字节范围内，那么前两个参数的传递就是通过两个寄存器来完成，余下的参数同`__stdcall`一样压栈，然后堆栈平衡由被调用者完成；其次函数名称的修饰规则是：前后都添加`@`，后接参数所需要的空间字节数大小（十进制，这里四个参数，因此是16个字节）。

## `thiscall`调用惯例

`thiscall`调用惯例是C++成员函数的默认调用惯例。

```cpp
class Node {
public:
    int get_sum(int a, int b)
    {
        return sum = a + b;
    }
    int sum;
};
int main(int argc, char** argv)
{
    Node node;
    node.get_sum(1, 2);
}
```

```assembly
class Node {
public:
    int get_sum(int a, int b)
    {
 push        ebp  
 mov         ebp,esp  
 sub         esp,8  
 mov         dword ptr [ebp-8],0CCCCCCCCh  
 mov         dword ptr [this],0CCCCCCCCh  
 mov         dword ptr [this],ecx  
        return sum = a + b;
 mov         eax,dword ptr [a]  
 add         eax,dword ptr [b]  
 mov         dword ptr [ebp-8],eax  
 mov         ecx,dword ptr [this]  
 mov         edx,dword ptr [ebp-8]  
 mov         dword ptr [ecx],edx  
 mov         eax,dword ptr [ebp-8]  
    }
 mov         esp,ebp  
 pop         ebp  
 ret         8  

int main(int argc, char** argv)
{
 push        ebp  
 mov         ebp,esp  
 sub         esp,0Ch  
 mov         dword ptr [ebp-0Ch],0CCCCCCCCh  
 mov         dword ptr [node],0CCCCCCCCh  
 mov         dword ptr [ebp-4],0CCCCCCCCh  
    Node node;
    node.get_sum(1, 2);
 push        2  
 push        1  
 lea         ecx,[node]  
 call        Node::get_sum (03011F9h)  
}
```

我们都知道，调用成员函数时，实际上是传递了`this`指针，那么`thiscall`有下列特征：

- 参数由右到左压入栈中，`this`指针传入`ecx`寄存器传递；
- 堆栈平衡由被调用者完成。

## 了解调用惯例的意义

调用惯例对于业务开发来说没有用，但是，如果需要看汇编，调试，或者开发一个库，这东西作用就很明显了。首先，如果不明白调用惯例，那么在反汇编的时候就是一头黑，根本看不同参数是如何传递的，也就不能顺藤摸瓜地找线索；其次，调试那种堆栈被破坏的程序，调用惯例能帮助我们找到堆栈平衡的地方，从而发现问题所在；最后，开发一个库，调用者和被调用者所遵守的调用惯例一定要一样，不然就会出现参数错位，堆栈不平衡等等问题。

------

参考：

- [Calling Conventions Demystified](https://www.codeproject.com/Articles/1388/Calling-Conventions-Demystified)

- [The history of calling conventions, part 3](https://devblogs.microsoft.com/oldnewthing/20040108-00/?p=41163)

- [x86 calling conventions](https://en.wikipedia.org/wiki/X86_calling_conventions)


